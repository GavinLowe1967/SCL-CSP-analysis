-- Check of an alt against its idealised model. 

-- Thread identities.  Thread AltThread will run the Alt; other threads will
-- act on channels.
datatype ThreadID = AltThread | T1 | T2 | T3 | T4 -- | T5

ChanThreads =  diff(ThreadID, {AltThread})

-- Identities of channels
datatype ChanID = Chan0 | Chan1

include "Types.csp"

-- Identities of Alts
datatype AltID = Alt1

include "Alt.csp"

-- The list of branches in the alt
branches =  <InPortBranch.Chan0, OutPortBranch.Chan1>
-- branches =  < InPortBranch.Chan0, InPortBranch.Chan1 >

Index = {0..length(branches)-1}

-- Create the alt.
instance A1 = Alt(Alt1, branches, ThreadID, ChanID)

-- Events corresponding to operation calls.  We allow all such events. 
ThreadEvents = union(
  A1::altInterface(AltThread), 
  Union({ A1::callBacks(t) | t <- ChanThreads })
)

-- Events that we don't try to constrain
FreeEvents = union(ThreadEvents, A1::RegistrationEvents)

-- Build the system.  We block events by threads not following their role
-- (channel thread or alt thread).
System0 = A1::runWithAndHide(RUN(FreeEvents)) 

System = System0[[
  A1::beginAlt <- IdealisedAlt::beginAlt,
  A1::endAlt <- IdealisedAlt::endAlt,
  -- Registration
  A1::beginRegisterIn <- IdealisedAlt::beginRegisterIn,
  A1::endRegisterIn <- IdealisedAlt::endRegisterIn,
  A1::beginRegisterOut <- IdealisedAlt::beginRegisterOut,
  A1::endRegisterOut <- IdealisedAlt::endRegisterOut,
  A1::beginDeregisterIn <- IdealisedAlt::beginDeregisterIn,
  A1::endDeregisterIn <- IdealisedAlt::endDeregisterIn,
  A1::beginDeregisterOut <- IdealisedAlt::beginDeregisterOut,
  A1::endDeregisterOut <- IdealisedAlt::endDeregisterOut,
  -- Call backs
  A1::beginMaybeSend <- IdealisedAlt::beginMaybeSend,
  A1::endMaybeSend <- IdealisedAlt::endMaybeSend,
  A1::beginMaybeReceive <- IdealisedAlt::beginMaybeReceive,
  A1::endMaybeReceive <- IdealisedAlt::endMaybeReceive,
  A1::beginPortClosed <- IdealisedAlt::beginPortClosed,
  A1::endPortClosed <- IdealisedAlt::endPortClosed
]]
[[ A1::spuriousWakeup <- IdealisedAlt::spuriousWakeup ]]
-- Note: the split of renamings is to make the FDR debug more readable.

include "IdealisedAlt.csp"

assert IdealisedAlt::SpecF [T= System

assert IdealisedAlt::SpecF [F= System

assert IdealisedAlt::SpecD [FD= System

-- The FD refinement implies the F refinement if the IdealisedAlt is
-- divergence free.

assert IdealisedAlt::IdealisedAlt :[ divergence free ]
